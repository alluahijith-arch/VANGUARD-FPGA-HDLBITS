module top_module(
    input clk,
    input areset,        // Freshly brainwashed Lemmings walk left.
    input bump_left,
    input bump_right,
    input ground,
    output walk_left,
    output walk_right,
    output aaah 
);

    // State encoding
    parameter LEFT = 2'b00, RIGHT = 2'b01, FALL_LEFT = 2'b10, FALL_RIGHT = 2'b11;

    reg [1:0] state, next_state;

    // Asynchronous reset + state register
    always @(posedge clk or posedge areset) begin
        if (areset)
            state <= LEFT;          // Start walking left
        else
            state <= next_state;
    end

    // Next-state logic
    always @(*) begin
        case (state)
            LEFT: begin
                if (!ground)                 // lack of ground, hence falls
                    next_state = FALL_LEFT;
                else if (bump_left)             // obstruction, hence has to change direction
                    next_state = RIGHT;
                else
                    next_state = LEFT;   // can continue left
            end

            RIGHT: begin
                if (!ground)
                    next_state = FALL_RIGHT;
                else if (bump_right)  
                    next_state = LEFT;
                else
                    next_state = RIGHT;
            end

            FALL_LEFT: begin
                if (ground)
                    next_state = LEFT; // no state for bump, as it does not affect the falling state
                else
                    next_state = FALL_LEFT;
            end

            FALL_RIGHT: begin
                if (ground)
                    next_state = RIGHT;
                else
                    next_state = FALL_RIGHT;
            end

            default: next_state = LEFT;
        endcase
    end

    // Output logic
    assign walk_left  = (state == LEFT);
    assign walk_right = (state == RIGHT);
    assign aaah       = (state == FALL_LEFT) || (state == FALL_RIGHT);

endmodule
